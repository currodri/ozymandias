module compute_halo_props

  use input_output
#ifdef STARS
  character(len=128)::inFileName='input_StarMaker.dat'
#else
  character(len=128)::inFileName='input_HaloMaker.dat'
#endif

  public

contains 

!//////////////////////////////////////////////////////////////////////////
!**************************************************************************
  subroutine init()

    implicit none  

    write_resim_masses = .true.

    ! initialize gravitational softening 
    call initgsoft()

    ! initialize cosmological and technical parameters of the simulation
    call init_cosmo()

    return

  end subroutine init

!*************************************************************************
  subroutine initgsoft

  ! subroutine to initialize the required arrays for the gravitational 
  ! field smoothing interpolation in routine SOFTGRAV. This is only
  ! required for a cubic spline kernel; interpolation is performed in 
  ! distance.

    implicit none

    integer(kind=4) :: i
    real(dp)    :: deldrg,xw,xw2,xw3,xw4,tiny,one,two

    tiny = real(1.e-19,kind=dp)
    one  = real(1.0,kind=dp)
    two  = real(2.0,kind=dp)

    if (gravsoft .eq. 'harmonic') return

    deldrg      = 2./ninterp
    phsmooth(0) = 7.*sqrt(tiny)/5.

    do i=1,1+ninterp
       xw  = i*deldrg
       xw2 = xw*xw
       xw3 = xw2*xw
       xw4 = xw2*xw2
       if (xw .le. one) then
          phsmooth(i) = -2.*xw3*(one/3.-3.*xw2/20.+xw3/20.)+7.*xw/5.
       else
          phsmooth(i) = -one/15.+8.*xw/5.-xw3*(4./3.-xw+3.*xw2/10.-xw3/30.)
       endif
       if (xw .ge. two) then
          phsmooth(i) = one
       endif
    enddo

    return

  end subroutine initgsoft

!*************************************************************************
  subroutine init_cosmo()

    ! This routine reads in the input_HaloMaker.dat (or
    ! input_StarMaker.dat) file which contains the cosmological 
    ! and technical parameters of the N-Body simulation to analyze.
    
    use neiKDtree
    use fof
    implicit none

    integer(kind=4)      :: i
    character(len=200)   :: line,name,value

    
    ! set default parameter values
    ! ----------------------------
    ai             = 1.0         ! Initial (beginning of the simulation) expansion factor
    omega_f        = 0.3333      ! mass density at final timestep
    omega_b_f      = -1.         ! baryon density
    omega_lambda_f = 0.6667      ! lambda at final timestep 
    af             = 36.587      ! expansion factor of the final timestep  
    Lf             = 150.0       ! final length of box in physical Mpc 
    H_f            = 66.667      ! Hubble constant in km/s/Mpc, at final timestep
    b_init         = 0.2         ! linking length friend-of-friend parameter @ z=0
    Nmembers       = 20          ! minimum number of particles for a halo
    nsteps         = 1           ! number of files to analyse (listed in 'files.dat')
    method         = "FOF"
    
    call subbox_defaults

    write(errunit,*) '> Values of input parameters:  '
    write(errunit,*) '> ---------------------------- '
    write(errunit,*) ''

    write(errunit,*) '> Looking for ',trim(inFileName),' in directory: ',trim(data_dir)
    call open_exist(20,trim(inFileName))
    do
       read (20,'(a)',end=2) line
       i = scan(line,'=')
       if (i == 0 .or. line(1:1) == '#') cycle
       name  = trim(adjustl(line(:i-1)))
       value = trim(adjustl(line(i+1:)))
       ! check for a comment at end of line !
       i     = scan(value,'!')
       if (i /= 0) value = trim(adjustl(value(:i-1)))
       if(verbose) write(errunit,'(1x,a1,a15,a3,a10)') '>',trim(name),' : ',trim(value)
       select case (trim(name))
       case ('omega_0' , 'Omega_0' , 'omega_f')
          read(value,*) omega_f
       case ('omega_l' , 'lambda_0' , 'lambda_f')
          read(value,*) omega_lambda_f
       case ('omega_b')
          read(value,*) omega_b_f
       case ('af' , 'afinal' , 'a_f')
          read(value,*) af
       case ('Lf' , 'lf' , 'lbox')
          read(value,*) Lf
       case ('H_f', 'H_0', 'H')
          read(value,*) H_f
       case('FlagPeriod')
          read(value,*) FlagPeriod
       case ('n', 'N', 'npart')
          read(value,*) nMembers
       case('cdm')
          read(value,*) cdm
       case('ssm')
          read(value,*) ssm
       case ('method' )
          write(method,'(a3)') trim(value)
       case ('b')
          read(value,*) b_init
       case ('nvoisins')
          read(value,*) nvoisins
       case ('nhop')
          read(value,*) nhop
       case ('rhot')
          read(value,*) rho_threshold
       case ('fudge')
          read(value,*) fudge
       case ('fudgepsilon')
          read(value,*) fudgepsilon
       case ('alphap')
          read(value,*) alphap
       case ('verbose') 
          read(value,*) verbose
       case ('megaverbose') 
          read(value,*) megaverbose
       case('nsteps','nsteps_do')
          read(value,*) nsteps
       case ('xmin')
          read(value,*) xmin_subbox
       case ('xmax')
          read(value,*) xmax_subbox
       case ('ymin')
          read(value,*) ymin_subbox
       case ('ymax')
          read(value,*) ymax_subbox
       case ('zmin')
          read(value,*) zmin_subbox
       case ('zmax')
          read(value,*) zmax_subbox
       case ('padlength','buffer')
          read(value,*) padlength        ! given by user in code units (comoving from 0 to 1)
       case ('danger','DangerPadLength') 
          read(value,*) dangerPadLength  ! size of danger zone inside the pad (same units as padlength)
       case ('subboxnum','subBoxNum')
          read(value,*) subboxnum        ! useful to define name of output file
       ! Tracer--
       case ('SimulationHasTracerParticles')
          read(value,*) SimulationHasTracerParticles
       ! --Tracer 
       case default
          write(errunit,*) 'dont recognise parameter: ',trim(name)
       end select
    end do
2   close (20)

    call check_subbox_params
    if (subboxFoF) then
       if (method .eq. 'BHM') then 
          write(errunit,*) 'STOP: BHM is not tested with sub-box option... '
          stop
       end if
#ifndef BIG_RUN 
       write(errunit,*) 'STOP: sub-box only checked for full (periodic) simulations ... (no resim)'
       write(errunit,*) ' ==> Use at your bloody own risk !'
#endif
    end if
    call define_padded_subbox

    if (((omega_f+omega_lambda_f) /= 1.0) .and. (omega_lambda_f /= 0.0)) then
       write(errunit,*) '> lambda + non flat Universe not implemented yet'
       stop
    endif

    ! In the most general of cases:
    !     af/aexp         = 1+z
    !     omega(z)        = (H_f/H(z))**2 * (1+z)**3  * omega_f
    !     omega_lambda(z) = omega_lambda_f * (H_f/H(z))**2
    !     omega_c(z)      = omega_c_f * (1+z)**2 * (H_f/H(z))**2
    !     H(z)**2         = H_f**2*( omega_f*(1+z)**3 + omega_c_f*(1+z)**2 + omega_lambda_f)

    omega_c_f = 1. - omega_f - omega_lambda_f
    H_i       = H_f * sqrt( omega_f*(af/ai)**3 + omega_c_f*(af/ai)**2 + omega_lambda_f)
    ! rho_crit = 2.78782 h^2  (critical density in units of 10**11 M_sol/Mpc^3)

    ! define mass of the box. Note that this mass is used to define background density (as Mbox/Lbox^3) which is 
    ! then used as the threshhold for detection (times rhot). 
#ifdef STARS
    if (omega_b_f < 0) then 
       write(errunit,*) 'Omega_b_f should be defined in parameter file ... '
       stop
    end if
    ! define baryonic mass of the box in 10^11 Msun
    mboxp     = 2.78782*(Lf**3)*(H_f/100.)**2*omega_b_f 
#else
    ! define total mass of the box (baryons + dark matter)
    ! NB: for a hydro simulation, one should really take omega_f - omega_b_f below. 
    mboxp     = 2.78782*(Lf**3)*(H_f/100.)**2*omega_f 
#endif

    ! jeje : the following line and comment is somewhat silly ... our convention is that ai=1 and for 
    ! gadget simulations we also have af = 1 ... -> lboxp = lf and that's about it.
    ! initial size of the box in physical Mpc (NB: f index stands for final quantities)
    Lboxp = Lf*(ai/af)

    write(errunit,*)
    write(errunit,*) '> Initial/Final values of parameters:  '
    write(errunit,*) '> -----------------------------------  '
    write(errunit,*)  
    write(errunit,*) '> redshift                         : ',af/ai-1.
    write(errunit,*) '> box size (Mpc)                   : ',Lboxp
    write(errunit,*) '> Hubble parameter  (km/s/Mpc)     : ',H_i
    write(errunit,*) '> box mass (10^11 Msol)            : ',mboxp
    write(errunit,*)

    return

  end subroutine init_cosmo

!*************************************************************************
  subroutine new_step()

  ! This is the main subroutine: it builds halos from the particle simulation and 
  ! computes their properties ...

    implicit none

    integer(kind=4)              :: indexp,ierr,i
    integer(kind=4)              :: found,n_halo_contam,n_subs_contam
    real(dp)                 :: read_time_ini,read_time_end
    real(dp)                 :: t0,t1
    logical                      :: printdatacheckhalo !put to true if bug after make_linked_list 
    integer(kind=4)              :: ih,subbox_nh,subbox_ns,isub,j
    logical,allocatable  :: in_the_box(:)
    ! leo
    real(dp)                 :: fhalo
    ! end leo
    ! jeje : 
#ifdef STARS
    integer(kind=4)              :: start
    integer(kind=4),allocatable  :: hnum(:)
#endif
    ! end jeje 

    write(errunit,'(1x,a16,i5)') '> Timestep  --->',numero_step
    write(errunit,*) '> -------------------'

    call cpu_time(read_time_ini)
    
    ! read N-body info for this new step
    call read_data
    
    ! rigault
!!$    open(unit=233,file='test_cat.dat',form='unformatted',status='unknown')
!!$    write(233) nbodies
!!$    write(233) pos(:,1)
!!$    write(233) pos(:,2)
!!$    write(233) pos(:,3)
!!$    write(233) vel(:,1)
!!$    write(233) vel(:,2)
!!$    write(233) vel(:,3)
!!$    close(233)
!!$    stop
    ! end rigault 

    
    ! determine the age of the universe and current values of cosmological parameters
    call det_age

    ! first compute the virial overdensity (with respect to the average density of the universe) 
    ! predicted by the top-hat collapse model at this redshift 
    call virial

    ! Leo: 
    ! use delta crit from Bryan&Norman98 fit to set rho_threshold
#ifdef BN98
    write(errunit,*) '> using B&N98 fit to set rho_threshold...'
    call fit_deltac
    fhalo = 1./3.    ! rho_halo_edge = fhalo * rho_halo_mean, fhalo ~ 1/3 assuming an isothermal profile 
    rho_threshold = fhalo * delta_crit_z / omega_z
    ! also, overwrite rho_mean and vir_overdens by rho_crit_z and delta_crit_z 
    ! used in det_vir_props to correct Mvir such that  mvir = vir_overdens*rho_mean*4./3.*pi*avir*bvir*cvir
    ! if BN98 then mvir = delta_crit_z*rho_crit_z*4./3.*pi*avir*bvir*cvir
    rho_mean = rho_crit_z
    vir_overdens = delta_crit_z
    write(errunit,*) '> rho_threshold, rho_crit, delta_crit :',rho_threshold,rho_crit_z,delta_crit_z
#endif
    write(errunit,*) '> rho_threshold               : ',rho_threshold
    write(errunit,*) '> rho_mean                    : ',rho_mean
    write(errunit,*) '> vir_overdens                : ',vir_overdens
    write(errunit,*)
    ! end leo

    if(method.ne."FOF") then
       allocate(liste_parts(1:nbodies),stat=ierr)
       if (ierr /= 0) then
          write(errunit,*) 'Cannot allocate liste_parts'
       endif
    end if

    if(nbodies .gt. 2*nvoisins) call make_halos ! Joki added crash security measure
    !call make_halos

    ! if there are no halos go to the next timestep
    if (nb_of_halos == 0) then
       write(errunit,*) 'no halos deallocating'
       deallocate(pos,vel)
       if(allocated(density)) deallocate(density)
       if(allocated(mass)) deallocate(mass)
       deallocate(liste_parts)
#ifdef STARS
       deallocate(stellarAge,stellarZ)
#endif
       print*,'no Halos, but writing tree_brick anyway'
       call write_tree_brick ! added by Joki
       return

    end if

    allocate(first_part(0:(nb_of_halos+nb_of_subhalos)),stat=ierr)
    if (ierr /= 0) then
       write(errunit,*) 'Cannot allocate first_part'
    endif

    allocate(nb_of_parts(0:(nb_of_halos+nb_of_subhalos)),stat=ierr)
    if (ierr /= 0) then
       write(errunit,*) 'Cannot allocate nb_of_parts'
    endif

    ! make a linked list of the particles so that each member of a halo points to the next 
    ! until there are no more members (last particles points to -1)
    allocate(linked_list(0:nbodies+1), stat=ierr)
    if (ierr /= 0) then
       write(errunit,*) 'Cannot allocate linked_list'
    endif
    call make_linked_list()

    ! deallocate liste_parts bc it has been replaced by linked_list
    deallocate(liste_parts)

    ! in case of resimulation (or individual particle masses) count how many halos are contaminated 
    ! (i.e. contain "low res" particles) 
    ! NB: for numerical reasons, it can happen that mass is sligthly larger 
    !     than massp. Thus, for detecting LR particles, the criterion 
    !     if (mass(indexp) > massp) ...  may not work (it can get ALL parts, also HR parts!).
    !     Thus, to be conservative, use: if (mass(indexp) > massp * (1+1e-5))) ...
#ifndef STARS
    if (allocated(mass)) then
       n_halo_contam = 0 
       n_subs_contam = 0  
       do i = 1,nb_of_halos+nb_of_subhalos
          indexp = first_part(i)
          found  = 0
          do while (indexp /= -1 .and. found == 0) 
             if (mass(indexp) > massp* 1.00001 ) then
                if(fsub) then
                   if (level(i) == 1) then 
                      n_halo_contam = n_halo_contam + 1
                   else
                      n_subs_contam = n_subs_contam + 1
                   endif
                else
                      n_halo_contam = n_halo_contam + 1
                end if
                found = 1
             endif
             indexp = linked_list(indexp)
          enddo
       enddo
       write(errunit,*) '> # of halos, # of CONTAMINATED halos :',nb_of_halos,n_halo_contam,nb_of_subhalos,n_subs_contam
       open(222,file='ncontam_halos.dat',status='unknown',position='append')
       write(222,'(5(i6,2x))') numero_step,nb_of_halos,n_halo_contam,nb_of_subhalos,n_subs_contam
       close(222)
    endif
#endif     

    ! allocation and initialization of the halo list
    allocate(liste_halos(0:(nb_of_halos+nb_of_subhalos)),stat=ierr)
    if (ierr /= 0) then
       write(errunit,*) 'Cannot allocate liste_halos'
       stop
    endif
    call init_halos

#ifdef CONTAM
    ! flag contaminated halos or sub-halos 
    do i = 1,nb_of_halos+nb_of_subhalos
       liste_halos(i)%contaminated = 0
       indexp = first_part(i)
       found  = 0
       do while (indexp /= -1 .and. found == 0) 
          if (mass(indexp) > massp* 1.00001 ) then
             liste_halos(i)%contaminated = 1
             found = 1
          end if
          indexp = linked_list(indexp)
       end do
    end do
    ! contaminate main halos which contain a contaminated halo ... 
    do i = 1, nb_of_halos 
       ih = i 
       do while (ih > 0) 
          if (liste_halos(ih)%contaminated ==1) liste_halos(i)%contaminated = 1
          ih = liste_halos(ih)%nextsub
       end do
    end do
    ! contaminate sub-halos which belong to a contaminated main halo ... 
    do i = 1,nb_of_halos
       if (liste_halos(i)%contaminated==1) then 
          ih = i
          do while (ih > 0) 
             liste_halos(ih)%contaminated = 1
             ih = liste_halos(ih)%nextsub
          end do
       end if
    end do
#endif

    ! flag main halos which have at least one pad-particle in any of their sub-halos (including main)
    if (subboxFoF) then 
       allocate(haloHasPadPart(nb_of_halos))
       haloHasPadPart = .false.
       do i = 1,nb_of_halos
          ih = i
          subhaloloop:do while (ih > 0) 
             indexp   = first_part(ih)
             do while (indexp /= -1)
                if (partInPad(indexp)) then 
                   haloHasPadPart(i) = .true.
                   exit subhaloloop
                end if
                indexp = linked_list(indexp)
             end do
             ih = liste_halos(ih)%nextsub
          end do subhaloloop
       end do
    end if

    ! jeje : define minPartID for all halos (and sub-halos)
    !      -> this is used to define uniquely sub-halos in case of degeneracy... 
    !       (i.e. when a same halo is found in two sub-boxes). 
    do ih = 1,nb_of_halos+nb_of_subhalos
       liste_halos(ih)%minPartID = nbodies_full_sim + 10
       indexp = first_part(ih)
       do while (indexp /= -1) 
          if (liste_halos(ih)%minPartID > particleID(indexp)) liste_halos(ih)%minPartID = particleID(indexp)
          indexp = linked_list(indexp)
       end do
    end do

    ! until now we were using code units for positions and velocities
    ! this routine changes that to physical (non comoving) coordinates for positions 
    ! and peculiar (no Hubble flow) velocities in km/s
    ! The masses are changed from code units into 10^11 M_sun as well.
    call change_units()

    ! jeje : i change ordering of things here because i need to know the positions of haloes in order
    !        to decide which ones are in the current sub-box and count them (and then write a proper 
    !        header to ang_mom_of_r file ...)
    ! -> hence the loop below, where we count good haloes

    ! leo: split this part into a big subboxFoF case (OpenMP & subboxFoF cases are mutually exclusive)

    if (subboxFoF) then     
       
       subbox_nh = 0
       subbox_ns = 0
       allocate(in_the_box(nb_of_halos + nb_of_subhalos))
       in_the_box = .false.
       
       do i = 1,nb_of_halos + nb_of_subhalos
          ! determine mass of halo       
          call det_mass(liste_halos(i))
          ! compute center of halo there as position of "most dense" particle
          ! and give it the velocity of the true center of mass of halo
          call det_center(liste_halos(i))
          if (fsub) then 
             ih = liste_halos(i)%hosthalo 
          else 
             ih = i
          end if
          if (.not. halo_is_in_the_box(liste_halos(ih))) then 
             cycle  ! skip (sub)halo if host is out of the sub-box
          else
             if (haloHasPadPart(ih)) then
                print*,'halo which is in the sub-box has dangerous particle ... increase buffer size ...'
                print*,i,ih
                stop
             end if
             if (ih == i) then 
                subbox_nh = subbox_nh + 1 
             else 
                subbox_ns = subbox_ns + 1
             end if
             in_the_box(i) = .true.
          end if
       end do
       write(errunit,*) '> SUBBOX: nb of halos and subhalos in the subbox:',subbox_nh,subbox_ns

       ! define re-numbering now in case of sub-box option
       ! -> will be used in input_output and when writing out the ang_mom_of_r file
       allocate(old2new(nb_of_halos + nb_of_subhalos))
       old2new = -1
       ih   = 0
       isub = subbox_nh
!!$       do i = 1,nb_of_halos
!!$          if (in_the_box(i)) then 
!!$             ih = ih + 1 
!!$             old2new(i) = ih 
!!$             j = liste_halos(i)%nextsub
!!$             do while (j > 0) 
!!$                isub = isub + 1
!!$                old2new(j) = isub
!!$                j = liste_halos(j)%nextsub
!!$             end do
!!$          end if
!!$       end do
       ! jeje 
       do i = 1,nb_of_halos+nb_of_subhalos
          if (in_the_box(i)) then 
             if (i <= nb_of_halos) then ! main halo 
                ih = ih + 1 
                old2new(i) = ih
             else ! sub-halo
                isub = isub + 1
                old2new(i) = isub
             end if
          end if
       end do
       ! end jeje 

       printdatacheckhalo = .false.
       do i = 1,nb_of_halos + nb_of_subhalos
          if(printdatacheckhalo) then
             write(errunit,*) '> halo:', i,'nb_of_parts',nb_of_parts(i)
             call cpu_time(t0)
          end if
          if (.not. in_the_box(i)) cycle ! skip (sub-)halo if host is out of the subbox
          if(printdatacheckhalo) write(errunit,*) '> center:',liste_halos(i)%p
          ! compute angular momentum of halos
          call compute_ang_mom(liste_halos(i))
          if(printdatacheckhalo) write(errunit,*) '> angular momentum:',liste_halos(i)%L
          ! compute r = max(distance of halo parts to center of halo)
          call r_halos(liste_halos(i))
          if(printdatacheckhalo) write(errunit,*) '> radius:',liste_halos(i)%r
          ! compute energies and virial properties of the halos depending on density profile
          ! (so this profile is also computed in the routine)
          call det_vir(liste_halos(i))
          if(printdatacheckhalo) write(errunit,*) '> mvir,rvir:',liste_halos(i)%datas%mvir,liste_halos(i)%datas%mvir
          ! compute dimensionless spin parameter of halos
          call compute_spin_parameter(liste_halos(i))
          if(printdatacheckhalo) write(errunit,*) '> spin:',liste_halos(i)%spin
          if(printdatacheckhalo) then
             call cpu_time(t1)
             write(errunit,*) '> halo computation took:',int(t1- t0) ,'s'
             write(errunit,*)
          end if
       end do

    else ! subboxFoF case

       ! leo: openmp: use of SCHEDULE to distribute packets ? 
!$OMP PARALLEL DO &
       !$OMP DEFAULT(SHARED) &
       !$OMP PRIVATE(i)
       do i = 1,nb_of_halos + nb_of_subhalos
          ! determine mass of halo       
          call det_mass(liste_halos(i))
          ! compute center of halo there as position of "most dense" particle
          ! and give it the velocity of the true center of mass of halo
          call det_center(liste_halos(i))
       end do
!$OMP END PARALLEL DO

       printdatacheckhalo = .false.

!$OMP PARALLEL DO &
       !$OMP DEFAULT(SHARED) &
       !$OMP PRIVATE(i)
       do i = 1,nb_of_halos + nb_of_subhalos
          if(printdatacheckhalo) then
             write(errunit,*) '> halo:', i,'nb_of_parts',nb_of_parts(i)
             call cpu_time(t0)
          end if
          if(printdatacheckhalo) write(errunit,*) '> center:',liste_halos(i)%p
          ! compute angular momentum of halos
          call compute_ang_mom(liste_halos(i))
          if(printdatacheckhalo) write(errunit,*) '> angular momentum:',liste_halos(i)%L
          ! compute r = max(distance of halo parts to center of halo)
          call r_halos(liste_halos(i))
          if(printdatacheckhalo) write(errunit,*) '> radius:',liste_halos(i)%r
          ! compute energies and virial properties of the halos depending on density profile
          ! (so this profile is also computed in the routine)
          call det_vir(liste_halos(i))
          if(printdatacheckhalo) write(errunit,*) '> mvir,rvir:',liste_halos(i)%datas%mvir,liste_halos(i)%datas%mvir
          ! compute dimensionless spin parameter of halos
          call compute_spin_parameter(liste_halos(i))
          if(printdatacheckhalo) write(errunit,*) '> spin:',liste_halos(i)%spin
          if(printdatacheckhalo) then
             call cpu_time(t1)
             write(errunit,*) '> halo computation took:',int(t1- t0) ,'s'
             write(errunit,*)
          end if
       end do
!$OMP END PARALLEL DO
    endif
    ! end leo

    if (subboxFoF) then 
       call write_tree_brick(nsub=subbox_ns,nhalo=subbox_nh,in_the_box=in_the_box)
       deallocate(in_the_box,old2new)
    else
       call write_tree_brick
    end if

#ifdef STARS 
    ! jeje : output all star positions, with halonumber to test selection
!!$    allocate(hnum(nbodies))
!!$    hnum = -1
!!$    do i = 1,nb_of_halos + nb_of_subhalos
!!$       start = first_part(i)
!!$       do j =1,nb_of_parts(i)  
!!$          hnum(start) = i
!!$          start = linked_list(start)
!!$       end do
!!$    end do
!!$    open(unit=133,file='stargroup.dat',form='unformatted',status='unknown')
!!$    write(133) nbodies
!!$    write(133) (hnum(i),i=1,nbodies)
!!$    write(133) (pos(i,1),i=1,nbodies)
!!$    write(133) (pos(i,2),i=1,nbodies)
!!$    write(133) (pos(i,3),i=1,nbodies)
!!$    close(133)
!!$    deallocate(hnum)
#endif

    if(numero_step.eq.1) then
       open(unit=123,form='formatted',status='unknown',file='info_run.tmp')
       write(123,*) ' massp    :',massp*1.e11
       write(123,*) ' mass min :', real(nMembers)*massp*1.e11
       write(123,*) ' Method   : ', method
       write(123,*) ' cdm      :',cdm
       write(123,*) ' ssm      :',ssm
       write(123,*) 'step,aexp,redshift,age_univ,nb_of_halos,nb_of_subhalos'
    end if
    write(123,'(1x,i3,3(1x,E16.4),2(1x,i10))') &
         numstep,aexp,af/aexp - 1.,age_univ,nb_of_halos,nb_of_subhalos
    if(numero_step.eq.nsteps) then
       close(123)
    end if
    
    if (subboxFof) deallocate(HaloHasPadPart)
    deallocate(liste_halos)
    deallocate(nb_of_parts,first_part,linked_list)
    deallocate(pos,vel)
    if(allocated(mass)) deallocate(mass)
#ifdef STARS
    deallocate(stellarAge,stellarZ)
#endif
    if(.not.cdm) deallocate(density)
    call cpu_time(read_time_end)

    write(errunit,*) '> time_step computations took : ',nint(read_time_end - read_time_ini),' seconds'
    write(errunit,*)

    return

  end subroutine new_step

!***********************************************************************
  subroutine make_halos()

  ! subroutine which builds the halos from particle data using fof or adaptahop

    use fof
    use neiKDtree
    implicit none 

    real(dp)    :: read_time_ini,read_time_end

    write(errunit,*) '> In routine make_halos '
    write(errunit,*) '> ----------------------'
    
    write(errunit,*)
    fPeriod    = real(FlagPeriod,kind=dp)
    if(FlagPeriod.eq.1) then
       write(errunit,*) '> WARNING: Assuming PERIODIC boundary conditions --> make sure this is correct'
       periodic = .true.
    else
       write(errunit,*) '> WARNING: Assuming NON PERIODIC boundary conditions --> make sure this is correct'
       periodic = .false.
       if (subboxFoF) then 
          write(errunit,*) 'Sub-box option assumes periodic conditions ... '
          stop
       end if
    end if
    
    if(numero_step.eq.1) then
       if(cdm.and.(.not.ssm)) then
          write(errunit,*) '> Center of haloes and subhaloes are defined as the particle the closest to the cdm'
       else if (ssm) then
          write(errunit,*) '> Center of haloes and subhaloes are defined using the shrinking sphere method'
       else
          write(errunit,*) '> Center of haloes and subhaloes are defined as the particle with the highest density' 
       end if
       select case(method)
       case("FOF")
          write(errunit,*) '> HaloMaker is using Friend Of Friend algorithm'
          call fof_init
          fsub = .false.
       case("HOP")
          write(errunit,*) '> HaloMaker is using Adaptahop in order to' 
          write(errunit,*) '> Detect halos, subhaloes will not be selected'    
          call init_adaptahop
          fsub = .false.
       case("DPM")
          write(errunit,*) '> HaloMaker is using Adaptahop in order to' 
          write(errunit,*) '> Detect halos, and subhaloes with the Density Profile Method'    
          call init_adaptahop
          fsub = .true.
       case("MSM")
          write(errunit,*) '> HaloMaker is using Adaptahop in order to' 
          write(errunit,*) '> Detect halos, and subhaloes with the Most massive Subhalo Method'
          call init_adaptahop
          fsub = .true.
       case("BHM")
          write(errunit,*) '> HaloMaker is using Adaptahop in order to' 
          write(errunit,*) '> Detect halos, and subhaloes with the Branch History Method'    
          call init_adaptahop
          fsub = .true.
       case default
          write(errunit,*) '> Selection method: ',method,' is not included'
          write(errunit,*) '> Please check input file:, ',trim(inFileName)
       end select
    end if
    
#ifdef Test_Gd
    write(errunit,*) '> end of test for read_gadget'
    stop
#endif
    call cpu_time(read_time_ini)
    if(method .eq.'FOF') then
       ! compute density when most dense particle is choosen as center
       call fof_main
       if(.not.cdm.and.nb_of_halos.gt.0) call compute_density_for_fof 
       nb_of_subhalos = 0       
    else
       call compute_adaptahop
       ! no nead to keep information about density when choosing particle closest to the cdm as center
       if(cdm) deallocate(density)
    end if
    call cpu_time(read_time_end)

    write(errunit,'(a,i3,a,i8)') ' > Number of halos with more than     ', nMembers,' particles:',nb_of_halos
    write(errunit,'(a,i3,a,i8)') ' > Number of sub-halos with more than ', nMembers,' particles:',nb_of_subhalos
    write(errunit,*) '> time_step computations took : ',nint(read_time_end - read_time_ini),' seconds'
  
    return

  end subroutine make_halos

!***********************************************************************
  subroutine make_linked_list()

  ! Subroutine builds a linked list of parts for each halo which 
  ! contains all its particles.

    implicit none

    integer(kind=4)             :: i,index1,index2,ierr
    integer(kind=4),allocatable :: current_ptr(:)  

    allocate(current_ptr(0:(nb_of_halos+nb_of_subhalos)),stat=ierr)
    if (ierr /= 0) then
       write(errunit,*) '> cannot allocate current_ptr in make_linked_list'
    endif

    ! initialization of linked list
    first_part(0:(nb_of_halos+nb_of_subhalos))  = -1
    nb_of_parts(0:(nb_of_halos+nb_of_subhalos)) =  0
    current_ptr(0:(nb_of_halos+nb_of_subhalos)) = -1
    linked_list(0:nbodies+1)                    = -1
 
    ! make linked list: a few (necessary) explanations ....
    !   1/ index1 (liste_parts(i)) is the number of the halo to which belongs particle i (i is in [1..nbodies]) 
    !   2/ first_part(j) is the number of the first particle of halo j  (j is in [1..nhalo])
    !   3/ current_ptr(index1) is the number of the latest particle found in halo number index1 
    ! to sum up, first_part(i) contains the number of the first particle of halo i,
    ! linked_list(first_part(i)) the number of the second particle of halo i, etc ... until 
    ! the last particle which points to number -1

    do i=1,nbodies
       index1 = liste_parts(i)
       if(index1.gt.(nb_of_halos+nb_of_subhalos)) stop 'error in liste_parts'
       if (first_part(index1) == -1) then
          first_part(index1)  = i
          nb_of_parts(index1) = 1
          current_ptr(index1) = i
       else
          index2              = current_ptr(index1)
          linked_list(index2) = i
          current_ptr(index1) = i
          nb_of_parts(index1) = nb_of_parts(index1)+1
       endif
    end do

    ! close linked list
    do i=0,(nb_of_halos + nb_of_subhalos)
       if (current_ptr(i) == -1) cycle
       index2              = current_ptr(i)
       linked_list(index2) = -1
    end do

    deallocate(current_ptr)

    return

  end subroutine make_linked_list

!*************************************************************************
  subroutine init_halos
    
    implicit none
    integer(kind=4) :: ihalo,ihtmp,imother

    do ihalo = 1, nb_of_halos + nb_of_subhalos

       call clear_halo(liste_halos(ihalo)) 
       liste_halos(ihalo)%my_number   = ihalo
       liste_halos(ihalo)%my_timestep = numstep
       if(fsub) then
          if(level(ihalo).eq.1) then
             if(first_daughter(ihalo).gt.0) then
                liste_halos(ihalo)%nextsub = first_daughter(ihalo)
             end if
             liste_halos(ihalo)%hosthalo   = ihalo
          else
             liste_halos(ihalo)%level    = level(ihalo)
             liste_halos(ihalo)%hostsub = mother(ihalo)
             imother = liste_halos(ihalo)%hostsub
             liste_halos(imother)%nbsub = liste_halos(imother)%nbsub + 1 
             if(first_daughter(ihalo).gt.0) then
                liste_halos(ihalo)%nextsub = first_daughter(ihalo)
             else if(first_sister(ihalo).gt.0) then
                liste_halos(ihalo)%nextsub = first_sister(ihalo)
             else
                ihtmp = ihalo
                do while((first_sister(ihtmp).le.0).and.(level(ihtmp).gt.1))
                   ihtmp = mother(ihtmp)
                end do
                if(level(ihtmp).gt.1) then
                   ihtmp = first_sister(ihtmp)
                   liste_halos(ihalo)%nextsub = ihtmp
                end if
             end if
             imother = liste_halos(ihalo)%hostsub
             imother = liste_halos(imother)%hosthalo
             if(liste_halos(imother)%level.ne.1) stop 'wrong id for halo host'
             liste_halos(ihalo)%hosthalo = imother
          end if
       end if
    end do
    
    if(fsub) then
       deallocate(mother,first_daughter,first_sister,level)
    end if
    return
    
  end subroutine init_halos

!*************************************************************************                  
  subroutine compute_density_for_fof

    use neikdtree
    implicit none

    omegaL   = omega_lambda_f
    omega0   = omega_f
    aexp_max = af
    hubble   = H_f*1e-2
    boxsize2 = Lf
    xlong    = boxsize2
    ylong    = xlong
    zlong    = xlong
    xlongs2  = xlong*0.5d0
    ylongs2  = ylong*0.5d0
    zlongs2  = zlong*0.5d0

    pos_renorm   = xlong
    pos_shift(1) = 0.0d0
    pos_shift(2) = 0.0d0
    pos_shift(3) = 0.0d0
    Hub_pt       = 100.*hubble * sqrt(omega0*(aexp_max/aexp)**3   & 
         &       + (1-omegaL-omega0)*(aexp_max/aexp)**2 + omegaL)

    nvoisins = nMembers
    nhop     = nMembers
    write(errunit,*) '> Computing density using adatahop routines'
    call change_pos
    ! action "neighbors
    call create_tree_structure
    call compute_mean_density_and_np
    call change_pos_back
    deallocate(iparneigh)

    return

  end subroutine compute_density_for_fof

!*************************************************************************
  subroutine det_mass(h)

    ! adds up masses of particles to get total mass of the halo. 
    
    implicit none
    integer(kind=4)    :: indexp,npch
    real(kind=8)       :: masshalo
    type(halo)         :: h
    
    masshalo      = 0d0
    npch          = 0
    indexp        = first_part(h%my_number)
    do while(indexp /= -1)
       if (allocated(mass)) then 
          masshalo = masshalo + real(mass(indexp),8)
       else
          masshalo = masshalo + real(massp,8) 
       endif
       npch = npch + 1
       indexp = linked_list(indexp)       
    end do
    h%m = real(masshalo,kind=dp)  ! in 10^11 M_sun

    if(npch.ne.nb_of_parts(h%my_number)) then
       write(errunit,*) '> Fatal error in det_mass for', h%my_number
       write(errunit,*) 'nb_of_parts, npch:',h%my_number,npch
       stop
    end if

    return

  end subroutine det_mass

!***********************************************************************
  subroutine compute_ang_mom(h)

  ! compute angular momentum of all halos

    implicit none

    integer(kind=4) :: indexp
    real(kind=8)    :: lx,ly,lz
    type (halo)     :: h
    type (vector)   :: dr,p

    ! we compute r * m * v, where r & v are pos and vel of halo particles relative to center of halo
    ! (particle closest to center of mass or most dense particle)

    indexp = first_part(h%my_number)
    lx =0d0 ; ly = 0d0 ; lz = 0d0
    do while(indexp /= -1)
       
       dr%x   = pos(indexp,1) - h%p%x
       dr%y   = pos(indexp,2) - h%p%y
       dr%z   = pos(indexp,3) - h%p%z
       
       call correct_for_periodicity(dr)
       
       if (allocated(mass)) then 
          p%x = mass(indexp)*(vel(indexp,1)-h%v%x)
          p%y = mass(indexp)*(vel(indexp,2)-h%v%y)
          p%z = mass(indexp)*(vel(indexp,3)-h%v%z)
       else
          p%x = massp*(vel(indexp,1)-h%v%x)
          p%y = massp*(vel(indexp,2)-h%v%y)
          p%z = massp*(vel(indexp,3)-h%v%z)
       endif

       lx  = lx + real(dr%y*p%z - dr%z*p%y,8)   ! in 10**11 Msun * km/s * Mpc
       ly  = ly + real(dr%z*p%x - dr%x*p%z,8)
       lz  = lz + real(dr%x*p%y - dr%y*p%x,8)        
       
       indexp = linked_list(indexp)   
       
    end do

    h%L%x = real(lx,kind=dp)
    h%L%y = real(ly,kind=dp)
    h%L%z = real(lz,kind=dp)

    return

  end subroutine compute_ang_mom

!***********************************************************************
  subroutine r_halos(h)

  ! compute distance of the most remote particle (with respect to center of halo, which
  ! is either center of mass or most bound particle)

    implicit none

    integer(kind=4) :: indexp
    real(dp)    :: dr2max,dr2
    type (vector)   :: dr
    type (halo)     :: h
 
    dr2max  = 0.0
    indexp = first_part(h%my_number)

    do while(indexp /= -1)
       
       dr%x = pos(indexp,1) - h%p%x
       dr%y = pos(indexp,2) - h%p%y
       dr%z = pos(indexp,3) - h%p%z         
       
       call correct_for_periodicity(dr)
       
       dr2    = (dr%x*dr%x + dr%y*dr%y + dr%z*dr%z)
       
       if (dr2 > dr2max) then
          dr2max         = dr2
       endif
       
       indexp=linked_list(indexp)   
       
    end do
    
    h%r = sqrt(dr2max)

    return

  end subroutine r_halos

!***********************************************************************
  subroutine det_center(h)

  ! compute position of center of mass of halo, and its velocity.

    implicit none

    type (halo)        :: h
    integer(kind=4)    :: indexp, icenter,ifirst 
    real(dp)       :: maxdens, distmin
    real(kind=8)       :: pcx,pcy,pcz,vcx,vcy,vcz
    type(vector)       :: dr,pc

    ! Shrinking spheres variables
    real(kind=8)       :: shrinkrad
    real(kind=4)       :: distmax,distcen
    real(kind=4)       :: shrinkfac
    real(kind=8)       :: pcxp,pcyp,pczp
    real(kind=8)       :: accmass

    icenter = -1

    shrinkfac = 0.90**2
    if (cdm .or. ssm) then

       ! compute cdm
       pcx   = 0d0 ; pcy   = 0d0 ; pcz = 0d0
       ifirst = first_part(h%my_number)
       indexp = ifirst
       do while (indexp /= -1) 
          dr%x = pos(indexp,1) - pos(ifirst,1)
          dr%y = pos(indexp,2) - pos(ifirst,2)
          dr%z = pos(indexp,3) - pos(ifirst,3)
          call correct_for_periodicity(dr)
          if (allocated(mass)) then
             pcx = pcx + real(mass(indexp)*dr%x,8)
             pcy = pcy + real(mass(indexp)*dr%y,8)
             pcz = pcz + real(mass(indexp)*dr%z,8)
          else
             pcx = pcx + real(massp*dr%x,8)
             pcy = pcy + real(massp*dr%y,8)
             pcz = pcz + real(massp*dr%z,8)
          end if
          indexp = linked_list(indexp)
       end do
       pcx  = pcx / real(h%m,8) + real(pos(ifirst,1),8)
       pcy  = pcy / real(h%m,8) + real(pos(ifirst,2),8)
       pcz  = pcz / real(h%m,8) + real(pos(ifirst,3),8)
       pc%x = real(pcx,kind=dp)
       pc%y = real(pcy,kind=dp)
       pc%z = real(pcz,kind=dp)
       call correct_for_periodicity(pc)
       ! search particule closest to the cdm
       indexp  = ifirst
       distmin = Lbox_pt
       distmax = 0.
       do while (indexp /= -1)
          dr%x = pos(indexp,1) - pc%x
          dr%y = pos(indexp,2) - pc%y
          dr%z = pos(indexp,3) - pc%z
          call correct_for_periodicity(dr)
          if (sqrt(dr%x**2+dr%y**2+dr%z**2).lt.distmin) then
             icenter = indexp
             distmin = sqrt(dr%x**2+dr%y**2+dr%z**2)
          end if
          ! Calculate maximum distance in the halo for SSM
          if (sqrt(dr%x**2+dr%y**2+dr%z**2).gt.distmax) then
             distmax = sqrt(dr%x**2+dr%y**2+dr%z**2)
          end if
          indexp = linked_list(indexp)
       end do
       if (ssm) then
          ! compute cdm through shrinking spheres method (SSM, Power et al. 2003)
          distmax = (distmax**2) * shrinkfac
          !          shrinkrad = 10*Lf / 2**20 ! Using a criterium based on resolution (10 elements for a 20 levels simulation)
          shrinkrad = distmax / (10**2)**4
          do while (distmax > shrinkrad)
             pcxp = pcx; pcyp = pcy; pczp = pcz   
             pcx   = 0d0 ; pcy   = 0d0 ; pcz = 0d0
             accmass = 0d0
             ifirst = first_part(h%my_number)
             indexp = ifirst 
             do while (indexp /= -1)
                distcen = (pos(indexp,1)-pcxp)**2 + (pos(indexp,2)-pcyp)**2 + (pos(indexp,3)-pczp)**2
                if (distcen .lt. distmax) then ! Include only particles within required radius
                   dr%x = pos(indexp,1) - pos(ifirst,1)
                   dr%y = pos(indexp,2) - pos(ifirst,2)
                   dr%z = pos(indexp,3) - pos(ifirst,3)
                   call correct_for_periodicity(dr) 
                   if (allocated(mass)) then
                      accmass = accmass + real(mass(indexp),8)
                      pcx = pcx + real(mass(indexp)*dr%x,8)
                      pcy = pcy + real(mass(indexp)*dr%y,8)
                      pcz = pcz + real(mass(indexp)*dr%z,8) 
                   else 
                      accmass = accmass + real(massp,8)
                      pcx = pcx + real(massp*dr%x,8) 
                      pcy = pcy + real(massp*dr%y,8) 
                      pcz = pcz + real(massp*dr%z,8) 
                   end if
                end if
                indexp = linked_list(indexp)
             end do
             if (accmass.gt.0.0) then
                pcx  = pcx / accmass + real(pos(ifirst,1),8)
                pcy  = pcy / accmass + real(pos(ifirst,2),8) 
                pcz  = pcz / accmass + real(pos(ifirst,3),8)
             else
                pcx  = pcx + real(pos(ifirst,1),8)
                pcy  = pcy + real(pos(ifirst,2),8) 
                pcz  = pcz + real(pos(ifirst,3),8)
             end if
             pc%x = real(pcx,4)   
             pc%y = real(pcy,4)     
             pc%z = real(pcz,4)      
             call correct_for_periodicity(pc)    
             ! search particule closest to the cdm   
             indexp  = ifirst     
             distmin = Lbox_pt        
             do while (indexp /= -1)        
                distcen = (pos(indexp,1)-pcxp)**2 + (pos(indexp,2)-pcyp)**2 + (pos(indexp,3)-pczp)**2 
                if (distcen .lt. distmax) then ! Include only particles within required radius
                   dr%x = pos(indexp,1) - pc%x   
                   dr%y = pos(indexp,2) - pc%y  
                   dr%z = pos(indexp,3) - pc%z             
                   call correct_for_periodicity(dr)   
                   if (sqrt(dr%x**2+dr%y**2+dr%z**2).lt.distmin) then    
                      icenter = indexp    
                      distmin = sqrt(dr%x**2+dr%y**2+dr%z**2)   
                   end if
                end if
                indexp = linked_list(indexp)
             end do
             distmax = distmax * shrinkfac  
          end do
       end if
    else
    
       maxdens = 0.0
       indexp  = first_part(h%my_number)
       do while (indexp /= -1)
          if (density(indexp).gt.maxdens) then
             maxdens = density(indexp)
             icenter = indexp
          end if
          indexp = linked_list(indexp)
       enddo

    end if

    if (icenter.lt.0) then
       write(errunit,*) '> Could not find a center for halo: ',h%my_number,icenter
       write(errunit,*) '  h%m,massp,h%m/massp             : ',h%m,massp,h%m/massp
       write(errunit,*) '  Lbox_pt,distmin                 : ',Lbox_pt,distmin
       write(errunit,*) '  pcx,pcy,pcz                  : ',pcx,pcy,pcz
       write(errunit,*) '  periodicity flag                : ',FlagPeriod
       write(errunit,*) '> Check routine det_center'
       stop
    end if

    h%p%x  = pos(icenter,1)
    h%p%y  = pos(icenter,2)
    h%p%z  = pos(icenter,3)

    ! velocity of center is set equal velocity of center of mass:

    indexp = first_part(h%my_number)
    vcx = 0d0 ; vcy = 0d0 ; vcz =0d0
    do while (indexp /= -1)

       if (allocated(mass)) then
          vcx = vcx + real(mass(indexp)*vel(indexp,1),8)
          vcy = vcy + real(mass(indexp)*vel(indexp,2),8)
          vcz = vcz + real(mass(indexp)*vel(indexp,3),8)
       else
          vcx = vcx + real(massp*vel(indexp,1),8)
          vcy = vcy + real(massp*vel(indexp,2),8)
          vcz = vcz + real(massp*vel(indexp,3),8)
       endif
       indexp   = linked_list(indexp)

    end do

    h%v%x = real(vcx,kind=dp)/h%m
    h%v%y = real(vcy,kind=dp)/h%m
    h%v%z = real(vcz,kind=dp)/h%m

    return

  end subroutine det_center
  
!***********************************************************************
  function interact(i,j)

    implicit none

    integer(kind=4) :: i,j,ifirst
    real(dp)    :: dist2ij
    real(dp)    :: interact,rinveff,r3inveff
    real(dp)    :: epstmp,massp2,lbox2
    type (vector)   :: dr

    save ifirst,epstmp,massp2
    data ifirst /1/

    if (ifirst == 1) then  
       ! epstmp is mean interparticular distance / 20.0 (i.e. smoothing length)
       ! true for tree code but RAMSES and ENZO ?
       epstmp = (massp/mboxp)**(1./3.) / 20.0
       massp2 = massp*massp
       ifirst = 0     
    end if

    Lbox2   = Lbox_pt**2
    dr%x    = pos(j,1) - pos(i,1)  
    dr%y    = pos(j,2) - pos(i,2)
    dr%z    = pos(j,3) - pos(i,3)
    call correct_for_periodicity(dr)
    dist2ij = (dr%x**2) + (dr%y**2) + (dr%z**2)
    dist2ij = max(dist2ij, tiny(0e0)**(1./3.))
    dist2ij = dist2ij / Lbox2

    if (allocated(mass)) then
       if (allocated(epsvect)) then
          call softgrav(epsvect(i),epsvect(j),dist2ij,rinveff,r3inveff)
          interact = -mass(i) * mass(j) * rinveff
       else
          ! do not correct for softening --> have to change that
          interact =-mass(i)*mass(j)/sqrt(dist2ij)
       endif
    else
       call softgrav(epstmp,epstmp,dist2ij,rinveff,r3inveff)
       interact = -massp2 * rinveff
    endif

    return

  end function interact

!***********************************************************************
  subroutine softgrav(epsp,epsi,drdotdr,rinveff,r3inveff)

  ! subroutine to compute the effective distance between particles of
  ! smoothing lengths, epsp and epsi, given their real distance**2, 
  ! drdotdr, in order to get the smoothed values of the potential and 
  ! acceleration phi and acc (in GRAVSUM). Calculations are for an 
  ! harmonic smoothing or a cubic spline smoothing kernel. 
  ! For the spline smoothing, phsmooth and acsmooth must have 
  ! been initialized by initgsoft.

    implicit none

    integer(kind=4) :: smindex
    real(dp)    :: epsp,epsi,drdotdr,sdrdotdr,rinveff,r3inveff,drdeldrg
    real(dp)    :: drsm,phsm,epseff,epsinv,dr,tiny,one 

    tiny = 1.e-19
    one  = 1.0

    if (gravsoft == 'harmonic') then

       drdotdr  = drdotdr+tiny
       rinveff  = 1.0/sqrt(drdotdr)
       r3inveff = rinveff*rinveff*rinveff
       epseff   = 0.5*(epsp+epsi)
       if (drdotdr .lt. epseff*epseff) then
          epsinv   = 1.0/epseff
          r3inveff = epsinv*epsinv*epsinv
          rinveff  = 1.5*epsinv-0.5*drdotdr*r3inveff
       endif

    else if (gravsoft == 'cubsplin') then

       dr       = epsp+epsi
       drdotdr  = drdotdr+tiny*0.25*dr**2
       sdrdotdr = sqrt(drdotdr)
       ! rinveff  = 1.0/sdrdotdr
       ! we never use r3inveff at the mo, disable it.  
       ! r3inveff = rinveff*rinveff*rinveff
       drdeldrg = sdrdotdr*ninterp/dr
       smindex  = drdeldrg
       ! if (ninterp .lt. smindex) smindex = ninterp
       ! if (one .lt. drdeldrg-smindex) then
       !    drsm = one
       ! else
       !    drsm = drdeldrg-smindex
       ! endif
       ! phsm = (1.-drsm)*phsmooth(smindex) + drsm*phsmooth(1+smindex)
       if (smindex > ninterp) then 
          phsm = phsmooth(1+ninterp)
       else
          if (one < drdeldrg-smindex) then
             phsm = phsmooth(1+smindex)
          else
             drsm = drdeldrg-smindex
             phsm = (1.-drsm)*phsmooth(smindex)+drsm*phsmooth(1+smindex)
          endif
       end if
       ! rinveff = phsm*rinveff
       rinveff = phsm/sdrdotdr
       ! NB: r3inveff is used to compute the acceleration and necessitates
       !     the definition of accsmooth which is not available here.  
       !     For the treecode, the relation should be:
       !     r3inveff=accsmooth*r3inveff (to be checked)

    endif

    return

  end subroutine softgrav

!***********************************************************************
  subroutine tab_props_inside(h,nr,tabm2,tabk2,tabp2,v,amax,bmax,cmax)

  ! returns the cumulative mass contained in concentric ellipsoids centered on the center of the
  ! halo (cdm or mbp)

    implicit none

    integer(kind=4)        :: nr,num_h,indexp,i,i_ell,louped_parts
    real(dp)           :: amax,bmax,cmax,v(3,3)
    real(kind=8)           :: tabm2(0:nr-1),tabk2(0:nr-1),tabp2(0:nr-1) !! double
    real(kind=8)           :: srm,srk                                   !! double
    real(dp)           :: rmax,dra,drb,drc
    real(dp)           :: r_ell,v2,rf
    real(dp),parameter :: epsilon = 1.e-2
    type (vector)          :: posp,vt
    type (halo)            :: h

    ! rescale to get ellipsoid  concentric to principal ellipsoid
    ! which contains all the particles of the halo
    rmax = 0.0
    indexp = first_part(h%my_number)
    do while(indexp.gt.0)
       posp%x = pos(indexp,1) - h%p%x
       posp%y = pos(indexp,2) - h%p%y
       posp%z = pos(indexp,3) - h%p%z
       call correct_for_periodicity(posp)
        ! project position vector along the principal ellipsoid axis
       dra    = posp%x*v(1,1)+posp%y*v(2,1)+posp%z*v(3,1)
       drb    = posp%x*v(1,2)+posp%y*v(2,2)+posp%z*v(3,2)
       drc    = posp%x*v(1,3)+posp%y*v(2,3)+posp%z*v(3,3)
       r_ell  = sqrt((dra / h%sh%a)**2 + (drb / h%sh%b)**2 + (drc / h%sh%c)**2)
       rmax   = max(rmax,r_ell)
       indexp = linked_list(indexp)
    end do
    
    amax = rmax * h%sh%a * (1.0 + epsilon)
    bmax = rmax * h%sh%b * (1.0 + epsilon)
    cmax = rmax * h%sh%c * (1.0 + epsilon)

    ! initialize loop quantities
    tabm2        = 0d0
    tabk2        = 0d0
    louped_parts = 0
    num_h        = h%my_number
    indexp       = first_part(num_h)

    do while (indexp /= -1)

       posp%x = pos(indexp,1) - h%p%x
       posp%y = pos(indexp,2) - h%p%y
       posp%z = pos(indexp,3) - h%p%z
       call correct_for_periodicity(posp)
       ! compute velocities in the halo frame adding in the Hubble flow
       vt%x   = vel(indexp,1) - h%v%x + posp%x * Hub_pt
       vt%y   = vel(indexp,2) - h%v%y + posp%y * Hub_pt
       vt%z   = vel(indexp,3) - h%v%z + posp%z * Hub_pt
       v2     = vt%x**2 + vt%y**2 + vt%z**2
       ! project position vector along the principal ellipsoid axis
       dra    = posp%x*v(1,1)+posp%y*v(2,1)+posp%z*v(3,1)
       drb    = posp%x*v(1,2)+posp%y*v(2,2)+posp%z*v(3,2)
       drc    = posp%x*v(1,3)+posp%y*v(2,3)+posp%z*v(3,3)
       ! biggest ellipsoid is divided in nr concentric ellipsoid shells: we
       ! calculate below the ellipsoid bin in which each particle falls and fill up the 
       ! mass and energy tables accordingly
       ! NB: if by chance the most distant particle from the center is ON the shortest
       !     axis, then r_ell is equal to 1-epsilon (one of the terms below is one and the others 0)
       !     otherwise r_ell is between 0 and 1-epsilon and so we just multiply it by nr to 
       !     find the ellipsoid shell containing the particle.
       r_ell  = sqrt((dra / amax)**2 + (drb / bmax)**2 + (drc / cmax)**2)
       i_ell  = int(r_ell*nr)
       if (i_ell .lt. nr) then 
          if (allocated(mass)) then 
             tabm2(i_ell) = tabm2(i_ell)+real(mass(indexp),8)
             tabk2(i_ell) = tabk2(i_ell)+0.5*real(mass(indexp),8)*v2
          else
             tabm2(i_ell) = tabm2(i_ell)+real(massp,8)
             tabk2(i_ell) = tabk2(i_ell)+0.5*real(massp,8)*v2
          endif
       else
          louped_parts = louped_parts + 1
       endif

       indexp = linked_list(indexp)

    end do

    if (louped_parts .gt. 0) then
       write(errunit,*) ''
       write(errunit,*) '> Problem in tab_props_inside : missed ',louped_parts,' particles'
       write(errunit,*) ''
       stop
    end if

    srm = tabm2(0)
    srk = tabk2(0)
    do i = 1,nr-1
       srm      = srm+tabm2(i)
       srk      = srk+tabk2(i)
       tabm2(i) = srm
       tabk2(i) = srk
       ! approximation based on appendix B of paper GALICS 1:
       ! better than 10-15 % accuracy on average
       tabp2(i) = -0.3 * gravconst * tabm2(i)**2 * rf(h%sh%a**2,h%sh%b**2,h%sh%c**2)
    end do
    ! correct potential energy estimate for small halos which are calculated by direct summation 
    if (h%ep /= tabp2(nr-1)) tabp2 = tabp2/tabp2(nr-1)*h%ep

    return

  end subroutine tab_props_inside

!***********************************************************************
  subroutine det_vir_props(h,v)

    ! computes the virial properties (radius, mass) of a halo

    implicit none

    integer(kind=4)           :: i,ii
    ! ttab = 1000 bins for virial radius precision better than 1% of halo size 
    integer(kind=4),parameter :: ttab = 1000 
    real(dp)              :: rvir,mvir,kvir,pvir,v(3,3)
    real(dp)              :: amax,bmax,cmax,avir,bvir,cvir
    real(kind=8)              :: tab_mass(0:ttab-1),tab_ekin(0:ttab-1),tab_epot(0:ttab-1)  !! double
    real(dp)              :: virth,virth_old,volmin
    type (halo)               :: h

    ! compute properties inside ttab concentric principal ellipsoids centered on center of halo
    call tab_props_inside(h,ttab,tab_mass,tab_ekin,tab_epot,v,amax,bmax,cmax)

    ! find the outermost ellipsoid bin where virial theorem is either satisfied better than 20 %
    ! or satisfied best ... 
    mvir      = tab_mass(ttab-1) 
    kvir      = tab_ekin(ttab-1) 
    pvir      = tab_epot(ttab-1)
    ! initialize rvir to be the geometric average of the axis radii of the outermost ellipsoid shell
    ! which contains at least one particle
    do i = ttab-1,1,-1
       if (tab_mass(i-1) < tab_mass(i)) exit
    enddo
    avir      = real(i,kind=dp)/real(ttab-1,kind=dp)*amax
    bvir      = real(i,kind=dp)/real(ttab-1,kind=dp)*bmax
    cvir      = real(i,kind=dp)/real(ttab-1,kind=dp)*cmax
    rvir      = (avir*bvir*cvir)**(1./3.)
    ! assume initial departure from virialization is 100 %
    virth_old = 1.0
    virth     = 1.0
    do i = ttab-1,0,-1
       ! if region is unbound, it cannot be virialized in the same time 
       if (tab_ekin(i)+tab_epot(i) >= 0.0) cycle
       ! region is bound so compute relative virialization |2*K+P|/|K+P|
       virth = abs((2.0*tab_ekin(i)+tab_epot(i))/(tab_ekin(i)+tab_epot(i)))
       ! if region is better virialized then update virial quantities
       if (virth < virth_old) then 
          mvir      = tab_mass(i) 
          ! take the min here bc initialization throws away all the empty outer shells
          avir      = min(avir,real(i,kind=dp)/real(ttab-1,kind=dp)*amax)
          bvir      = min(bvir,real(i,kind=dp)/real(ttab-1,kind=dp)*bmax)
          cvir      = min(cvir,real(i,kind=dp)/real(ttab-1,kind=dp)*cmax)
          rvir      = (avir*bvir*cvir)**(1./3.)
          kvir      = tab_ekin(i) 
          pvir      = tab_epot(i)
          virth_old = virth
          ! if virial theorem holds with better than 20 % accuracy, exit do loop
          if (virth <= 0.20) exit
       endif
    end do
 
    ! for small halos it may happen that virial theorem is not enforced to within 15 %
    ! bc the halo is not fully virialized yet or that it is valid by fluke (right combination
    ! of potential and kinetic energy) ... so .... 
    ! 1/ in the latter case, we further check that the halo density is high enough 
    ! (order of what predicted by the spherical top-hat model, vir_overdens) 
    ! for us to believe in the measurement of the virial theorem. 
    ! 2/ in the former case, as an inner region measurement of the virialization would be too noisy for 
    ! lack of mass resolution (not enough particles) we only use the overdensity criterion.
    ! NB: this criterion is similar to NFW but with vir_overdens * average density and NOT 200. * critical density
    !     which does not makes sense when Omega_matter(z) /= 1 (not critical) and we take ellipsoids
    !     NOT spheres bc they yield more accurate volumes and average halo densities
    ! average density of the universe at current timestep (in 10^11 M_sun / Mpc^3)
 
    ! volume of the smallest concentric ellipsoid
    volmin   = 4./3.*pi*(amax/real(ttab-1,kind=dp))*(bmax/real(ttab-1,kind=dp))*(cmax/real(ttab-1,kind=dp))
    if (virth > 0.20 .or. mvir < vir_overdens*rho_mean*4./3.*pi*avir*bvir*cvir) then
       do ii = ttab-1,1,-1
          ! assume that the virial mass and radii are obtained when the density inside the ellipsoid 
          ! is greater than vir_overdens * rho_mean AND there is at least one particle inside the outermost 
          ! ellipsoid shell 
          mvir = vir_overdens * rho_mean * volmin * real(ii,kind=dp)**3
          if (tab_mass(ii) >= mvir .and. tab_mass(ii-1) < tab_mass(ttab-1)) exit
       enddo
       mvir   = tab_mass(ii)
       kvir   = tab_ekin(ii)
       pvir   = tab_epot(ii)
       avir   = real(ii,kind=dp)/real(ttab-1,kind=dp)*amax
       bvir   = real(ii,kind=dp)/real(ttab-1,kind=dp)*bmax
       cvir   = real(ii,kind=dp)/real(ttab-1,kind=dp)*cmax
       rvir   = (avir*bvir*cvir)**(1./3.)
    endif

    ! check if virialization conditions were met --> if not set relevant quantities to zero: this is a 
    ! non-virialized halo ....
    if (mvir > 0.0 .and. rvir > 0.0) then 
       ! it may happen (although it is very rare bc vector linking center of halo to most distant 
       ! particle has to be roughly parallel to minor axis of the halo in such a case) that the virial 
       ! radius estimated from the geometric mean of the 3 principal axis is slightly larger than the 
       ! distance of the most distant particle to the center of the halo (what we call r)
       ! when this happens we set the virial radius to be r
       h%datas%rvir         = min(rvir,h%r)    ! in Mpc
       h%datas%mvir         = mvir             ! in 10^11 M_sun
       ! circular velocity at r_vir in km/s
       h%datas%cvel         = sqrt(gravconst*h%datas%mvir/h%datas%rvir) 
       ! temperature at r_vir in K
       h%datas%tvir         = 35.9*gravconst*h%datas%mvir/h%datas%rvir
       ! compute halo density profile within the virialized region
       call compute_halo_profile(h)
    else
       write(*,*) 'halo bugged',h%my_number,mvir,rvir
       stop
    endif

    return 

  end subroutine det_vir_props

!***********************************************************************
  subroutine compute_halo_profile(h)

    implicit none

    type (halo)     :: h

    if (profile == 'TSIS') then
       ! for the singular isothermal sphere the profile is defined @ rvir for it is singular at r=0.
       h%halo_profile%rho_0 = h%datas%mvir / (4.0 * pi * h%datas%rvir**3)
       h%halo_profile%r_c   = h%datas%rvir
    else
       write(errunit,*) 'Other profiles than TSIS not yet fully implemented'
       stop
    endif

    return 

  end subroutine compute_halo_profile

!***********************************************************************
  subroutine change_units

    ! subroutine which goes from positions in code units to physical (non comoving)
    ! Mpc and from velocities in code units to peculiar (no Hubble flow) velocities 
    ! in km/s. masses are also changed from code units to 10^11 M_sun

    implicit none

    pos   = pos * Lbox_pt
    if (type == 'SN') vel = vel*Hub_pt*Lbox_pt
    massp = massp * mboxp
    if (allocated(mass)) mass  = mass * mboxp
#ifdef STARS    
    if (allocated(massInit)) massInit  = massInit * mboxp
#endif
    if(verbose) then
       write(errunit,*) '> xmin,xmax:',minval(pos(:,1)),maxval(pos(:,1))
       write(errunit,*) '> ymin,ymax:',minval(pos(:,2)),maxval(pos(:,2))
       write(errunit,*) '> zmin,zmax:',minval(pos(:,3)),maxval(pos(:,3))
       write(errunit,*) '> particle mass:', massp*1.e11
    end if
    
    if (subboxFoF) then 
       call convert_subbox_units
    end if

    return

  end subroutine change_units

!***********************************************************************
  function temps(x,omm,oml)

    implicit none

    real(dp) :: temps,x,omm,oml

    temps = 1./sqrt(omm*x**5+(1.0-omm-oml)*x**4+oml*x**2)

    return

  end function temps

!***********************************************************************
  function temps_turn_around(x,omm,oml)

    implicit none

    real(dp) :: temps,temps_turn_around,x,omm,oml

    temps = omm*x**5+(-omm-oml)*x**4+oml*x**2
    if (temps > 0.0) then 
       temps_turn_around = 1./sqrt(temps)
    else
       temps = 0.0
    endif

    return

  end function temps_turn_around

!***********************************************************************
  function dtda_turn_around(x,omm,oml)

    ! find dt/da given a, from the Friedmann Equation.  
    ! here, time "t" is understood to be in units of the inverse Hubble constant
    ! (i.e. "t" = H0*t)
    ! define the curvature so that turnaround occurs for a = 1.0  
    ! Note that omega_matter+omega_lambda+omega_curvature sum to ZERO, not one, in that case
    ! definitions for parameters are as in Peebles 1993, eqn (5.53)

    implicit none

    real(dp) :: x,omm,oml,temp,dtda_turn_around

    temp = omm*x + oml/x**2 - (omm+oml)
    if (temp .gt. 0.0) then
       dtda_turn_around = sqrt(1./temp)
    else
       dtda_turn_around = 0.0
    end if
    
    return

  end function dtda_turn_around

!***********************************************************************
  subroutine det_vir(h)

  ! determine virial properties of the halos, energies and profiles
  
    implicit none

    type (halo)     :: h
    real(dp)    :: v(3,3)

    ! compute principal axis of halo
    call det_main_axis(h,v)
 
    ! compute halo energies if necessary i.e. in the case where the center of the halo is the 
    ! center of mass bc if it is the most bound particle then energies are already available  
    call det_halo_energies(h)

    ! compute virial properties based on conditions necessary for the virial theorem to apply
     call det_vir_props(h,v)

    return

  end subroutine det_vir

!***********************************************************************
  subroutine det_age()

  ! subroutine which determines the current age of the Universe (Gyr) and
  ! values of current cosmological parameters 
 
    implicit none

    external midpnt
    real(dp) :: age0,age1,somme0,somme1,omm,oml
    save age0

    omm  = omega_f
    oml  = omega_lambda_f
    ! age0 is the age of the universe @ the beginning of the simulation (in Gyr)
    call qromo(temps,omm,oml,(af/ai),real(10001.,kind=dp),somme0,midpnt) 
    age0 = 977.78*somme0/H_f            

    ! age1 is the time between the beginning of the simulation and the current timestep (in Gyr)
    if (aexp /= ai) then 
       call qromo(temps,omm,oml,(af/aexp),(af/ai),somme1,midpnt)
    else
       somme1 = 0.0
    endif
    age1 = 977.78*somme1/H_f 

    ! finally the age of the universe is the sum 
    age_univ = age0+age1  

    write(errunit,*)
    write(errunit,*) '> Current values of parameters: '
    write(errunit,*) '> ----------------------------- '
    write(errunit,*)
    write(errunit,*) '> Redshift                    : ',af/aexp-1.
    write(errunit,*) '> Age of the Universe (Gyr)   : ',age_univ

    Hub_pt   = H_f * sqrt(omega_f*(af/aexp)**3 +  omega_c_f*(af/aexp)**2 + omega_lambda_f)
    Lbox_pt  = Lboxp*(aexp/ai)
    Lbox_pt2 = Lbox_pt / 2.0

    write(errunit,*) '> Hubble Parameter  (km/s/Mpc): ',Hub_pt
    write(errunit,*) '> Box Length (Mpc)            : ',Lbox_pt
    write(errunit,*)

    return

  end subroutine det_age

!***********************************************************************
  subroutine virial()

    ! compute the overdensity factor for virialization in a tophat collapse model at a given redshift 
    ! for a given cosmological model
    
    implicit none
    
    real(dp) :: a,b,eta,omega_maxexp,age,reduce,cubic,UnDixMoinsSix=1d-6
    
    ! convert age of universe from Gyr back into inverse Hubble parameter units
    age       = age_univ/977.78*H_f
    ! compute the overdensity needed to reach maximum expansion by half the age of the universe
    omega_maxexp = omega_f
    call collapse(age/2.0,omega_maxexp,UnDixMoinsSix)
    ! calculate how far an object collapses to virial equilibrium
    eta = 2.0*omega_lambda_f/omega_maxexp*(af/aexp)**3
    if (eta == 0.0) then
       reduce = 0.5
    else
       a      = 2.0*eta
       b      = -(2.0+eta)
       print*,'calling cubic from virial()'
       reduce = cubic(a,real(0.0,kind=dp),b,real(1.0,kind=dp))
       print*,'done ... '
    end if
    
    vir_overdens = omega_maxexp/omega_f/reduce**3*(aexp/af)**3
    rho_mean     = mboxp/Lbox_pt**3

    return
    
  end subroutine virial

!***********************************************************************
  subroutine collapse(age0,omm,acc)
    ! this subroutine performs a brute-force search using bracketing to find the value of the cosmic curvature 
    ! that gives turnaround at the specified expansion parameter. The expansion parameter is defined to
    ! be 1 at turnaround.
    !
    ! note that for a constant cosmological constant, the age at a given
    ! expansion factor decreases monotonically with omegam (before turnaround;
    ! after, there is more than one age at a given expansion factor).
    !
    ! third argument is the desired fractional accurracy.

    implicit none
    
    real(dp)            :: age0,omm,acc,oml
    real(dp)            :: age,omax,omin,age_f
    real(dp), parameter :: omax0=1.e7,omin0=1.e0 
    ! IMPORTANT NOTE: omax0 corresponds to a perturbation turning around at z=140 in a LCDM standard cosmology
    !                 and needs to be increased if you analyze outputs before this redshift ...
  
    external midpnt
    
    age  = -1.0  ! impossible value
    omax = omax0
    omin = omin0
    oml  = omega_lambda_f
    do while (abs(age-age0) .gt. acc*age0 .and. omax-omin .gt. acc*omm)
       omm = 0.5*(omax+omin)
       call qromo(temps_turn_around,omm,oml,real(101,kind=dp),real(10001,kind=dp),age_f,midpnt)
       call qromo(temps_turn_around,omm,oml,real(1,kind=dp),real(101,kind=dp),age,midpnt)
       if (age+age_f .gt. age0) then
          omin = omm
       else
          omax = omm
       end if
    end do

    if (omax .eq. omax0 .or. omin .eq. omin0) then
       write (errunit,*) 'WARNING: presumed bounds for omega are inadequate in collapse.'
       write (errunit,*) 'WARNING: omax,omax0,omin,omin0=',omax,omax0,omin,omin0
    end if
    
    return
    
  end subroutine collapse
 
!***********************************************************************
  subroutine det_halo_energies(h)

    implicit none

    integer(kind=4)           :: indexp,indexpp,np
    integer(kind=4),parameter :: full_PE = 1000 ! below this number of parts, we calculate full potential energy 
    real(dp)              :: v2,rf          ! rf is elliptic integral function from numrec
    real(kind=8)              :: ped,ked        ! need hi precision for potential and ke energy sum.   
    logical(dp)           :: count_pairs
    type (halo)               :: h
    type (vector)             :: vt,dr

    np          = nb_of_parts(h%my_number)
    count_pairs = (np < full_PE)
    ! get potential energy 
    if (.not.count_pairs) then 
       ! formula B1 of appendix of paper GalICS 1 :
       ! EP = -3/5 G M^2 Rf, 
       ! with Rf = 0.5 * Int_0^infty dt/sqrt((t+x)(t+y)(t+z)) = 0.5 * rf_numrec
       ! NB : rf_numrec returns RF in inverse Mpc (because x is in Mpc^2)
       h%ep = (-0.3) * gravconst * h%m**2 * rf(h%sh%a**2,h%sh%b**2,h%sh%c**2)
    else
       indexp = first_part(h%my_number)
       ped    = 0d0
       do while (indexp /= -1)              
          indexpp = linked_list(indexp) ! only count pairs once
          do while (indexpp /= -1)
             ped     = ped + real(interact(indexp,indexpp),8)
             indexpp = linked_list(indexpp)
          end do
          indexp = linked_list(indexp)
       end do
       h%ep = real(ped,kind=dp) * gravconst / Lbox_pt
    end if

    ! get kinetic energy (in center-of-halo frame)
    indexp = first_part(h%my_number)
    ked    = 0d0
    do while (indexp /= -1)   
       vt%x = vel(indexp,1) - h%v%x
       vt%y = vel(indexp,2) - h%v%y
       vt%z = vel(indexp,3) - h%v%z
       dr%x = pos(indexp,1) - h%p%x
       dr%y = pos(indexp,2) - h%p%y
       dr%z = pos(indexp,3) - h%p%z
       call correct_for_periodicity(dr)
       ! add Hubble flow 
       vt%x = vt%x + dr%x * Hub_pt
       vt%y = vt%y + dr%y * Hub_pt
       vt%z = vt%z + dr%z * Hub_pt
       v2   = vt%x**2 + vt%y**2 + vt%z**2
       if (allocated(mass)) then 
          ked = ked + real(mass(indexp)*v2,8)
       else
          ked = ked + real(massp*v2,8)
       endif
       indexp  = linked_list(indexp)
    end do
    h%ek = 0.5*real(ked,kind=dp)
    ! get total energy 
    h%et = h%ek + h%ep
    
    return
    
  end subroutine det_halo_energies

!***********************************************************************
  subroutine compute_spin_parameter(h)

    implicit none

    real(dp)                :: hl,spin
    type (halo)                 :: h

    hl                  = h%L%x**2 + h%L%y**2 + h%L%z**2
    hl                  = sqrt(hl)        
    spin                = hl * sqrt(abs(h%et)) / h%m**2.5
    spin                = spin / gravconst
    h%spin              = spin

    return

  end subroutine compute_spin_parameter

!***********************************************************************
  subroutine det_inertial_tensor(h,mat)

  ! Compute inertial tensor with respect to center of halo (either cdm or mbp)

    implicit none

    integer(kind=4) :: num_h,indexp
    real(dp)    :: mat(1:3,1:3)
    real(kind=8)    :: md(1:3,1:3)
    type (vector)   :: dr
    type (halo)     :: h

    num_h  = h%my_number
    md     = 0d0
    indexp = first_part(num_h)   

    do while (indexp /= -1)

       dr%x=pos(indexp,1)-h%p%x
       dr%y=pos(indexp,2)-h%p%y
       dr%z=pos(indexp,3)-h%p%z

       call correct_for_periodicity(dr)

       if (allocated(mass)) then 
          md(1,1) = md(1,1) + real(mass(indexp)*dr%x*dr%x,8)  
          md(1,2) = md(1,2) + real(mass(indexp)*dr%x*dr%y,8)   
          md(1,3) = md(1,3) + real(mass(indexp)*dr%x*dr%z,8)  
          md(2,1) = md(2,1) + real(mass(indexp)*dr%x*dr%y,8)         
          md(2,2) = md(2,2) + real(mass(indexp)*dr%y*dr%y,8)  
          md(2,3) = md(2,3) + real(mass(indexp)*dr%y*dr%z,8)      
          md(3,1) = md(3,1) + real(mass(indexp)*dr%x*dr%z,8)  
          md(3,2) = md(3,2) + real(mass(indexp)*dr%y*dr%z,8)    
          md(3,3) = md(3,3) + real(mass(indexp)*dr%z*dr%z,8)    
       else
          md(1,1) = md(1,1) + real(massp*dr%x*dr%x,8)  
          md(1,2) = md(1,2) + real(massp*dr%x*dr%y,8)   
          md(1,3) = md(1,3) + real(massp*dr%x*dr%z,8)  
          md(2,1) = md(2,1) + real(massp*dr%x*dr%y,8)         
          md(2,2) = md(2,2) + real(massp*dr%y*dr%y,8)  
          md(2,3) = md(2,3) + real(massp*dr%y*dr%z,8)      
          md(3,1) = md(3,1) + real(massp*dr%x*dr%z,8)  
          md(3,2) = md(3,2) + real(massp*dr%y*dr%z,8)    
          md(3,3) = md(3,3) + real(massp*dr%z*dr%z,8)  
       endif

       indexp = linked_list(indexp)        

    end do

    mat(1,1) = real(md(1,1),kind=dp)
    mat(1,2) = real(md(1,2),kind=dp)
    mat(1,3) = real(md(1,3),kind=dp)
    mat(2,1) = real(md(2,1),kind=dp)
    mat(2,2) = real(md(2,2),kind=dp)
    mat(2,3) = real(md(2,3),kind=dp)
    mat(3,1) = real(md(3,1),kind=dp)
    mat(3,2) = real(md(3,2),kind=dp)
    mat(3,3) = real(md(3,3),kind=dp)



    return

  end subroutine det_inertial_tensor

!***********************************************************************
  subroutine det_main_axis(h,v)

  ! determine the principal axis of the halo (h%sh%a,b,c)

    implicit none

    integer(kind=4) :: nrot
    real(dp)    :: mat(1:3,1:3)
    real(dp)    :: d(3),v(3,3)
    type (halo)     :: h


    call det_inertial_tensor(h,mat)

    call jacobi(mat,3,d,v,nrot)

    d      = sqrt(d/h%m)
    h%sh%a = d(1)
    h%sh%a = max(h%sh%a, tiny(0e0)**(1./3.))
    h%sh%b = d(2)
    h%sh%b = max(h%sh%b, tiny(0e0)**(1./3.))
    h%sh%c = d(3)
    h%sh%c = max(h%sh%c, tiny(0e0)**(1./3.))
 
    return

  end subroutine det_main_axis
  
!***********************************************************************
  subroutine fit_deltac

    ! compute delta_crit(z) using the fit form from Bryan&Norman(1998)

    implicit none
    
    real(dp)    :: z,E2z,x,rho_crit
    
    z = af/aexp-1.
    E2z = omega_f * (1.+z)**3. + omega_lambda_f
    omega_z = omega_f * (1.+z)**3. / E2z
    x = omega_z - 1.

    delta_crit_z = (18*pi**2 + 82.*x - 39*x**2)

    rho_crit = 2.78782*(H_f/100.)**2  ! (critical density at z=0 in units of 10**11 M_sol/Mpc^3)
    rho_crit_z = rho_crit * E2z

    return

  end subroutine fit_deltac
    
!***********************************************************************
!///////////////////////////////////////////////////////////////////////

end module compute_halo_props
